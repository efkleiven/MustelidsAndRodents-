
    model{
    
    ###########################################
    ### Spatial Dynamic Co-Occurrence Model ###
    ###########################################
    # State 1= Unoccupied(U), State 2= rodent(A), State 3 = mustelid(B), State 4 = rodent & mustelid(AB)  
    ########################################### 
   
    ##############
    ##  Priors  ##    
    ##############

    for(i in 1:2){
      beta0_gamA[i]  ~ dnorm(0,1)
      beta0_gamAB[i] ~ dnorm(0,1)
      beta0_gamB[i]  ~ dnorm(0,1)
      beta0_gamBA[i] ~ dnorm(0,1)
    
      beta0_epsA[i]  ~ dnorm(0,1)
      beta0_epsAB[i] ~ dnorm(0,1)
      beta0_epsB[i]  ~ dnorm(0,1)
      beta0_epsBA[i] ~ dnorm(0,1)
      
      beta1_gamA[i]  ~ dnorm(0,1)
      beta1_gamAB[i] ~ dnorm(0,1)
      beta1_gamB[i]  ~ dnorm(0,1)
      beta1_gamBA[i] ~ dnorm(0,1)
    
      beta1_epsA[i]  ~ dnorm(0,1)
      beta1_epsAB[i] ~ dnorm(0,1)
      beta1_epsB[i]  ~ dnorm(0,1)
      beta1_epsBA[i] ~ dnorm(0,1)
      
      beta0_GamA[i]  ~ dnorm(0,1)
      beta0_GamAB[i] ~ dnorm(0,1)
      beta0_GamB[i]  ~ dnorm(0,1)
      beta0_GamBA[i] ~ dnorm(0,1)
    
      beta0_EpsA[i]  ~ dnorm(0,1)
      beta0_EpsAB[i] ~ dnorm(0,1)
      beta0_EpsB[i]  ~ dnorm(0,1)
      beta0_EpsBA[i] ~ dnorm(0,1)
      
      
  # interscept det prob
    alphaA0[i] ~ dnorm(0,1)
    alphaB0[i] ~ dnorm(0,1)
     } # end loop
    
    # prior for the seasonal covariat on block level  
    for(b in 1:nblock){
      for(t in 1:(nseason-1)){
        seas_block[b,t] ~ dbern(0.5)
        
        for(j in 1:nsite){
          seas[j,b,t] ~ dbern(0.5)
          }
        }
      }  
      

    
  # initial state parameters
    for(b in 1:nblock){
      for(i in 1:3){
        psi[b,i] ~ dunif(0,0.5) # site
        }
  
      # for block, which is just a function of the states of the sites within each block
   
      x[b,1] <- ifelse(sum(z[,b,1]==1) == nsite, 1,
                 ifelse(sum(z[,b,1]==2) + sum(z[,b,1]==1) == nsite, 2,
                  ifelse(sum(z[,b,1]==3) + sum(z[,b,1]==1) == nsite, 3, 4) ) )
                  
                  
      #site 
      for(j in 1:nsite){
        fsm[j, b, 1] <- 1-psi[b,1]-psi[b,2]-psi[b,3]  #-----------|U
        fsm[j, b, 2] <- psi[b,1]                      #-----------|A
        fsm[j, b, 3] <- psi[b,2]                      #-----------|B
        fsm[j, b, 4] <- psi[b,3]                      #-----------|AB

    
    # first season latent state
    # for sites   
      z[j, b, 1] ~ dcat( fsm[j, b, ( 1:nout )]) 
      } #close site loop
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
  
  
    ###############################################
    # btpm = block transition probability matrix. #
    # All columns sum to 1.                       #
    ###############################################
     for(t in 1:(nseason-1)){
     
    # U to ...
    btpm[b,t, 1, 1] <- (1-GamA[b,t]) * (1-GamB[b,t])    #--|U
    btpm[b,t, 2, 1] <- GamA[b,t] * (1-GamB[b,t])        #--|A
    btpm[b,t, 3, 1] <- (1-GamA[b,t]) * GamB[b,t]        #--|B
    btpm[b,t, 4, 1] <- GamA[b,t] * GamB[b,t]            #--|AB
    
    # A to ...
    btpm[b,t, 1, 2] <- EpsA[b,t] * (1-GamBA[b,t])       #--|U
    btpm[b,t, 2, 2] <- (1-EpsA[b,t]) * (1-GamBA[b,t])   #--|A
    btpm[b,t, 3, 2] <- EpsA[b,t] * GamBA[b,t]           #--|B
    btpm[b,t, 4, 2] <- (1-EpsA[b,t]) * GamBA[b,t]       #--|AB
    
    # B to ...
    btpm[b,t, 1, 3] <- (1-GamAB[b,t]) * EpsB[b,t]       #--|U
    btpm[b,t, 2, 3] <- GamAB[b,t] * EpsB[b,t]           #--|A
    btpm[b,t, 3, 3] <- (1-GamAB[b,t]) * (1-EpsB[b,t])   #--|B
    btpm[b,t, 4, 3] <- GamAB[b,t] * (1-EpsB[b,t])       #--|AB
    
    # AB to ..
    btpm[b,t, 1, 4] <- EpsAB[b,t] * EpsBA[b,t]          #--|U
    btpm[b,t, 2, 4] <- (1-EpsAB[b,t]) * EpsBA[b,t]      #--|A
    btpm[b,t, 3, 4] <- EpsAB[b,t] * (1-EpsBA[b,t])      #--|B
    btpm[b,t, 4, 4] <- (1-EpsAB[b,t]) * (1-EpsBA[b,t])  #--|AB

    
   # latent block state for the rest of the seasons
    x[b, t+1] ~ dcat(btpm[b, t, (1:nout), x[b, t]])
    
    
    ####################################################
    ## stpm = site transition probability matrix.     ##
    ## These are dependent on the block level state   ##
    ## All columns sum to 1.                          ##
    ####################################################
 
 ## Logit links for col and ext probabilities
 
    logit(GamA[b,t])  <- beta0_GamA[seas_block[b,t]+1] 
    logit(GamAB[b,t]) <- beta0_GamAB[seas_block[b,t]+1] 
    logit(GamB[b,t])  <- beta0_GamB[seas_block[b,t]+1] 
    logit(GamBA[b,t]) <- beta0_GamBA[seas_block[b,t]+1] 
 
    logit(EpsA[b,t])  <- beta0_EpsA[seas_block[b,t]+1] 
    logit(EpsAB[b,t]) <- beta0_EpsAB[seas_block[b,t]+1] 
    logit(EpsB[b,t])  <- beta0_EpsB[seas_block[b,t]+1] 
    logit(EpsBA[b,t]) <- beta0_EpsBA[seas_block[b,t]+1]
    

   for(j in 1:nsite){
      #~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
      # logit links

      logit(gamA[b, j, t])  <- beta0_gamA[hab[b, j]]  + beta1_gamA[seas[j, b, t]+1] 
      logit(gamAB[b, j, t]) <- beta0_gamAB[hab[b, j]] + beta1_gamAB[seas[j, b, t]+1] 
      logit(gamB[b, j, t])  <- beta0_gamB[hab[b, j]]  + beta1_gamB[seas[j, b, t]+1] 
      logit(gamBA[b, j, t]) <- beta0_gamBA[hab[b, j]] + beta1_gamBA[seas[j, b, t]+1]
 
      logit(epsA[b, j, t])  <- beta0_epsA[hab[b, j]]  + beta1_epsA[seas[j, b, t]+1] 
      logit(epsAB[b, j, t]) <- beta0_epsAB[hab[b, j]] + beta1_epsAB[seas[j, b, t]+1]
      logit(epsB[b, j, t])  <- beta0_epsB[hab[b, j]]  + beta1_epsB[seas[j, b, t]+1]
      logit(epsBA[b, j, t]) <- beta0_epsBA[hab[b, j]] + beta1_epsBA[seas[j, b, t]+1]
  

  # site transition matrix   
    # blocks state (x) = U
    
      # U to ...
      stpm[b, j, t, 1, 1, 1] <- 1          #--|U
      stpm[b, j, t, 2, 1, 1] <- 0          #--|A
      stpm[b, j, t, 3, 1, 1] <- 0          #--|B
      stpm[b, j, t, 4, 1, 1] <- 0          #--|AB
    
      # A to ...
      stpm[b, j, t, 1, 2, 1] <- 1          #--|U
      stpm[b, j, t, 2, 2, 1] <- 0          #--|A
      stpm[b, j, t, 3, 2, 1] <- 0          #--|B
      stpm[b, j, t, 4, 2, 1] <- 0          #--|AB
    
      # B to ...
      stpm[b, j, t, 1, 3, 1] <- 1           #--|U
      stpm[b, j, t, 2, 3, 1] <- 0           #--|A
      stpm[b, j, t, 3, 3, 1] <- 0           #--|B
      stpm[b, j, t, 4, 3, 1] <- 0           #--|AB
    
      # AB to ..
      stpm[b, j, t, 1, 4, 1] <- 1           #--|U
      stpm[b, j, t, 2, 4, 1] <- 0           #--|A
      stpm[b, j, t, 3, 4, 1] <- 0           #--|B
      stpm[b, j, t, 4, 4, 1] <- 0           #--|AB

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

 # blocks state (x) = A
  
# U to ...
    stpm[b, j, t, 1, 1, 2] <- (1-gamA[b, j, t])    #--|U
    stpm[b, j, t, 2, 1, 2] <- gamA[b, j, t]        #--|A
    stpm[b, j, t, 3, 1, 2] <- 0                    #--|B
    stpm[b, j, t, 4, 1, 2] <- 0                    #--|AB
    
    # A to ...
    stpm[b, j, t, 1, 2, 2] <- epsA[b, j, t]        #--|U
    stpm[b, j, t, 2, 2, 2] <- (1-epsA[b, j, t])    #--|A
    stpm[b, j, t, 3, 2, 2] <- 0                    #--|B
    stpm[b, j, t, 4, 2, 2] <- 0                    #--|AB
    
    # B to ...
    stpm[b, j, t, 1, 3, 2] <- (1-gamAB[b, j, t])   #--|U
    stpm[b, j, t, 2, 3, 2] <- gamAB[b, j, t]       #--|A
    stpm[b, j, t, 3, 3, 2] <- 0                    #--|B
    stpm[b, j, t, 4, 3, 2] <- 0                    #--|AB
    
    # AB to ..
    stpm[b, j, t, 1, 4, 2] <- epsAB[b, j, t]       #--|U
    stpm[b, j, t, 2, 4, 2] <- (1-epsAB[b, j, t])   #--|A
    stpm[b, j, t, 3, 4, 2] <- 0                    #--|B
    stpm[b, j, t, 4, 4, 2] <- 0                    #--|AB

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

 # blocks state (x) = B
 
# U to ...
    stpm[b, j, t, 1, 1, 3] <-  (1-gamB[b, j, t])    #--|U
    stpm[b, j, t, 2, 1, 3] <- 0                     #--|A
    stpm[b, j, t, 3, 1, 3] <-  gamB[b, j, t]        #--|B
    stpm[b, j, t, 4, 1, 3] <- 0                     #--|AB
    
    # A to ...
    stpm[b, j, t, 1, 2, 3] <- (1-gamBA[b, j, t])    #--|U
    stpm[b, j, t, 2, 2, 3] <- 0                     #--|A
    stpm[b, j, t, 3, 2, 3] <- gamBA[b, j, t]        #--|B
    stpm[b, j, t, 4, 2, 3] <- 0                     #--|AB
    
    # B to ...
    stpm[b, j, t, 1, 3, 3] <-  epsB[b, j, t]        #--|U
    stpm[b, j, t, 2, 3, 3] <- 0                     #--|A
    stpm[b, j, t, 3, 3, 3] <- (1-epsB[b, j, t])     #--|B
    stpm[b, j, t, 4, 3, 3] <- 0                     #--|AB
    
    # AB to ..
    stpm[b, j, t, 1, 4, 3] <-  epsBA[b, j, t]       #--|U
    stpm[b, j, t, 2, 4, 3] <- 0                     #--|A
    stpm[b, j, t, 3, 4, 3] <-  (1-epsBA[b, j, t])   #--|B
    stpm[b, j, t, 4, 4, 3] <- 0                     #--|AB

 
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

 # blocks state (x) = AB
 
# U to ...
    stpm[b, j, t, 1, 1, 4] <- (1-gamA[b, j, t]) * (1-gamB[b, j, t])    #--|U
    stpm[b, j, t, 2, 1, 4] <- gamA[b, j, t]  * (1-gamB[b, j, t])       #--|A
    stpm[b, j, t, 3, 1, 4] <- (1-gamA[b, j, t]) * gamB[b, j, t]        #--|B
    stpm[b, j, t, 4, 1, 4] <- gamA[b, j, t] * gamB[b, j, t]            #--|AB
    
    # A to ...
    stpm[b, j, t, 1, 2, 4] <- epsA[b, j, t] * (1-gamBA[b, j, t])       #--|U
    stpm[b, j, t, 2, 2, 4] <- (1-epsA[b, j, t]) * (1-gamBA[b, j, t])   #--|A
    stpm[b, j, t, 3, 2, 4] <- epsA[b, j, t] * gamBA[b, j, t]           #--|B
    stpm[b, j, t, 4, 2, 4] <- (1-epsA[b, j, t]) * gamBA[b, j, t]       #--|AB
    
    # B to ...
    stpm[b, j, t, 1, 3, 4] <- (1-gamAB[b, j, t] ) * epsB[b, j, t]      #--|U
    stpm[b, j, t, 2, 3, 4] <- gamAB[b, j, t]  * epsB[b, j, t]          #--|A
    stpm[b, j, t, 3, 3, 4] <- (1-gamAB[b, j, t] ) * (1-epsB[b, j, t])  #--|B
    stpm[b, j, t, 4, 3, 4] <- gamAB[b, j, t]  * (1-epsB[b, j, t])      #--|AB
    
    # AB to ..
    stpm[b, j, t, 1, 4, 4] <- epsAB[b, j, t] * epsBA[b, j, t]          #--|U
    stpm[b, j, t, 2, 4, 4] <- (1-epsAB[b, j, t]) * epsBA[b, j, t]      #--|A
    stpm[b, j, t, 3, 4, 4] <- epsAB[b, j, t] * (1-epsBA[b, j, t])      #--|B
    stpm[b, j, t, 4, 4, 4] <- (1-epsAB[b, j, t]) * (1-epsBA[b, j, t])  #--|AB

  #~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    
      # latent site state for the rest of the seasons
        z[j, b, t+1] ~ dcat( stpm[b, j, t, ( 1:nout ) , z[ j, b, t], x[b,t+1]] + 0.01)  # +0.01 to avoide giving the dcat a prob of 0 
       
          for(day in 1:nsurvey) {      
            y[j, b, t, day] ~ dcat( dpm[b, j, t, ( 1:nout ) , z[j, b, t]] + 0.01)  # +0.01 to avoide giving the dcat a prob of 0 
          } #end survey loop

    
    #############################################################
    ## detection matrix (OS = observed state, TS = true state) ##
    #############################################################
    
    # TS = U
    dpm[b, j, t, 1, 1] <- 1                                    #--|OS = U
    dpm[b, j, t, 2, 1] <- 0                                    #--|OS = A
    dpm[b, j, t, 3, 1] <- 0                                    #--|OS = B
    dpm[b, j, t, 4, 1] <- 0                                    #--|OS = AB
    
    # TS = A
    dpm[b, j, t, 1, 2] <- 1-pA[b, j, t]                       #--|OS = U
    dpm[b, j, t, 2, 2] <- pA[b, j, t]                         #--|OS = A
    dpm[b, j, t, 3, 2] <- 0                                    #--|OS = B
    dpm[b, j, t, 4, 2] <- 0                                    #--|OS = AB
    
    # TS = B
    dpm[b, j, t, 1, 3] <- 1-pB[b, j, t]                       #--|OS = U
    dpm[b, j, t, 2, 3] <- 0                                    #--|OS = A
    dpm[b, j, t, 3, 3] <- pB[b, j, t]                         #--|OS = B
    dpm[b, j, t, 4, 3] <- 0                                    #--|OS = AB
    
    # TS = AB
    dpm[b, j, t, 1, 4] <- (1-pA[b, j, t]) * (1-pB[b, j, t])  #--|OS = U
    dpm[b, j, t, 2, 4] <- pA[b, j, t] * (1-pB[b, j, t])      #--|OS = A
    dpm[b, j, t, 3, 4] <- (1-pA[b, j, t]) * pB[b, j, t]      #--|OS = B
    dpm[b, j, t, 4, 4] <- pA[b, j, t] * pB[b, j, t]          #--|OS = AB
    
    ## logit links for detection probs
    logit(pA[b, j, t]) <- alphaA0[seas[j, b, t]+1] 
    logit(pB[b, j, t]) <- alphaB0[seas[j, b, t]+1]  
    
      } # end site loop
        } # end time loop
          } #close block loop
    
    ## Derived parameters
    
    diff_gamA[1:2,1:2] <- gamA[1, c(1,10), c(1,10)] - gamAB[1, c(1,10), c(1,10)]
    diff_gamB[1:2,1:2] <- gamB[1, c(1,10), c(1,10)] - gamBA[1, c(1,10), c(1,10)]
    diff_epsA[1:2,1:2] <- epsA[1, c(1,10), c(1,10)] - epsAB[1, c(1,10), c(1,10)]
    diff_epsB[1:2,1:2] <- epsB[1, c(1,10), c(1,10)] - epsBA[1, c(1,10), c(1,10)]
    
    diff_GamA[1:2] <- GamA[1,c(1,10)] - GamAB[1,c(1,10)]
    diff_GamB[1:2] <- GamB[1,c(1,10)] - GamBA[1,c(1,10)]
    diff_EpsA[1:2] <- EpsA[1,c(1,10)] - EpsAB[1,c(1,10)]
    diff_EpsB[1:2] <- EpsB[1,c(1,10)] - EpsBA[1,c(1,10)]
    
    ratio_gamA[1:2,1:2] <- gamA[1, c(1,10), c(1,10)] / gamAB[1, c(1,10), c(1,10)]
    ratio_gamB[1:2,1:2] <- gamB[1, c(1,10), c(1,10)] / gamBA[1, c(1,10), c(1,10)]
    ratio_epsA[1:2,1:2] <- epsAB[1, c(1,10), c(1,10)] / epsA[1, c(1,10), c(1,10)]
    ratio_epsB[1:2,1:2] <- epsB[1, c(1,10), c(1,10)] / epsBA[1, c(1,10), c(1,10)]
    
    ratio_GamA[1:2] <- GamA[1,c(1,10)] / GamAB[1,c(1,10)]
    ratio_GamB[1:2] <- GamBA[1,c(1,10)] / GamB[1,c(1,10)]
    ratio_EpsA[1:2] <- EpsAB[1,c(1,10)] / EpsA[1,c(1,10)]
    ratio_EpsB[1:2] <- EpsB[1,c(1,10)] / EpsBA[1,c(1,10)]    
    
    }# end
    
